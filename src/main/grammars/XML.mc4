/*
 * ******************************************************************************
 * MontiCore Language Workbench, www.monticore.de
 * Copyright (c) 2017, MontiCore, All rights reserved.
 *
 * This project is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3.0 of the License, or (at your option) any later version.
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this project. If not, see <http://www.gnu.org/licenses/>.
 * ******************************************************************************
 */


grammar XML extends de.monticore.literals.MCCommonLiterals {

  /** The starting nonterminal. */
  start XMLDocument;
  
  /** An ASTXMLValue represents an arbitrary property value. */
  interface XMLValue;
  
  /** An ASTXMLDocument represents an input file in XML format.
    @attribute name The name of the XML document.
    @attribute values The top level values contained in the XML document.
  */
  XMLDocument = node:XNode*;
  
  XNode = "<" name:Name Attribute* ( "/>" | ">"
    (node:XNode+ | value:XMLValue+)
  "</" endKey:Name ">");
  
  /** An ASTXMLAttribute represents a key-value pair.
    @key value The key of the attribute.
    @value value The actual value of the attribute.
  */
  Attribute = key:Name "=" value:XMLValue;
  
  /** An ASTXMLBoolean represents a boolean property value.
    @attribute value The actual value.
  */
  XMLBoolean implements XMLValue = value:BooleanLiteral;

  /** An ASTXMLString represents a String property value.
    @attribute value The actual value.
  */
  XMLString implements XMLValue = value:String;
  
  /** An ASTXMLNames represents a concatenation of multiple names.
    @attribute value The actual value.
  */
  XMLNames implements XMLValue = value:Name+;

  /** An ASTXMLNumber represents a number property value.
    @attribute value The actual value (as signed integer or double).
  */
  XMLNumber implements XMLValue = value:SignedNumericLiteral;

  /** An ASTXMLArray represents an array of multiple property values.
    @attribute values The values in the array.
  */
  XMLArray implements XMLValue = "[" value:(XMLValue || ",")* "]";

  /** An ASTXMLNull represents an empty property value.
    @attribute value The actual "null" value.
  */
  XMLNull implements XMLValue = value:NullLiteral;
  
  
}
